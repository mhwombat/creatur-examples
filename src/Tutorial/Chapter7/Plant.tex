The species used in this example is called |Plant|.
Each |Plant| has a unique ID, a flower colour,
an energy level, and some genetic information.
(A complete listing of the source code discussed here is provided on 
page \pageref{code:plant}.)

\begin{code}
data Plant = Plant
  { 
    plantName :: String,
    plantFlowerColour :: FlowerColour,
    plantEnergy :: Int,
    plantGenome :: Sequence
  } deriving (Show, Generic)
\end{code} 

As with |Rock|s, the type |Plant| will be an instance of 
the |Serialize|, |Agent| and |Record| classes.
Our plants will stay alive until all of their energy is gone.

\begin{code}
instance Serialize Plant

instance Agent Plant where
  agentId = plantName
  isAlive plant = plantEnergy plant > 0

instance Record Plant where key = agentId
\end{code} 

We'll have a choice of flower colours.
\begin{code}
data FlowerColour = Red | Orange | Yellow | Violet | Blue
  deriving (Show, Eq, Generic, Enum, Bounded)
\end{code} 

In order for |Plant| to be an instance of |Serialize|,
any type that it uses must also be an instance.
So we make |FlowerColour| be an instance of |Serialize|.

\begin{code}
instance Serialize FlowerColour
\end{code} 

We need a way to encode the plant genes into DNA-like sequences that can
be shuffled, or even mutated, during reproduction.
We'll encode the genes as sequences of |Bool|s.
We could write our own coding scheme, but \path{ALife.Creatur.Genetics.Code.BRGCBool}
provides a scheme for us, using a class called |Gene|.
The |Gene| class provides the method |put|, which encodes a gene and writes it to a sequence,
and the method |get|, which reads and decodes the first gene in a sequence.
By making |FlowerColour| an instance of |Gene|, 
|FlowerColour| will be encoded as a string of boolean values using a \emph{Gray code}.
A Gray code maps values to codes in a way that guarantees that the codes
for two consecutive values will differ by only one bit. This feature
is useful for encoding genes because the result
of a crossover operation will be similar to the inputs. 
This helps to
ensure that offspring are similar to their parents, as any radical
changes from one generation to the next are the result of mutation
alone.

When the genes of an agent have a small set of possible values,
it is practical to store their genetic information as a string of |Bool|s.
(If an agent has genes with a larger number of possible values,
it may be better to store their genetic information as a string of numbers.
Créatúr also provides \path{ALife.Creatur.Genetics.Code.BRGCWord8},
which encodes the genes using a Gray code, but stores them using a string of |Word8|s.
Since |BRGCWord8| provides most of the same functions as |BRGCBool|,
all we need to do to make |Plant| use |Word8|s is to import |BRGCWord8|
instead of |BRGCBool|.)
We'll see an example of this in Section \ref{sec:species4}.

\begin{code}
instance Genetic FlowerColour
\end{code} 

To support reproduction, we need a way to build a plant from its genome.
First, each plant needs a copy of its genome in order to produce offspring;
we'll use the |copy| method to obtain this.
Next, we determine the colour of the bug.
We could use the method |get| in the class |Gene|, 
which returns a |Maybe| value containing the next gene in a sequence.
However, our sequence of |Bool|s may not
be a valid code for a colour, in which case the call to |get|
would return |Nothing|.
In this example, we will create a plant no matter what errors there are in 
the genome, so we will use |getWithDefault|, using |Red| as the default value.
(Alternatively, we could treat the mutation as non-viable,
and not create the offspring.
We'll see an example of that in Section \ref{sec:species4}.
All plants start life with an energy of |10|.
        
\begin{code}
buildPlant :: String -> Reader (Maybe Plant)
buildPlant name = do
  g <- copy
  colour <- getWithDefault Red
  return . Just $ Plant name colour 10 g
\end{code} 

We need a way to mate two plants and produce some offspring.
We can do this by implementing the |Reproductive| class in 
\path{ALife.Creatur.Genetics.Reproduction.Asexual}.
This class requires us to implement the following:
\begin{enumerate}
\item A type called |Base|, which specifies the type used to encode
genes for this species. Recall that we've used |Bool|s for this purpose.
\item A method called |recombine| which shuffles (and maybe mutates) the
 parent's genes to produce the offspring.
\item A method called |build| which creates the offspring.
We can call the |read| method in the |Reproductive| class,
supplying the |buildPlant| method as an argument.
\end {enumerate}

\begin{code}
instance Reproductive Plant where
  type Base Plant = Sequence
  recombine a b = 
    withProbability 0.1 randomCrossover (plantGenome a, plantGenome b) >>=
    withProbability 0.01 randomCutAndSplice >>=
    withProbability 0.001 mutatePairedLists >>=
    randomOneOfPair
  build name = runReader (buildPlant name)
\end{code} 

The implementation for |recombine| uses the sample recombination
program discussed on page \pageref{code:recombination}.
Next, we write the function |run|, 
which is invoked when it is the agent's turn to use the CPU.
Because our plants need to interact in order to mate,
when we write the daemon (in Section \ref{sec:daemon2}) we will use
|runInteractingAgents| instead of |runNoninteractingAgents|.
This requires a different type signature for |run| than we used for
rocks.
The type signature we need is

\begin{code}
type AgentsProgram c l d n x a = 
  [a] -> StateT (Universe c l d n x a) IO [a]
\end{code} 

The input parameter is a list of agents. 
The first agent in the list is the agent whose turn it is to use the 
CPU.
The rest of the list contains agents it could interact with.
(We only need to use the first two elements of this list.)
Finally, the program must return a list of agents that it has modified.

The function |run| ``mates'' two plants and takes away one unit of energy 
to represent the energy cost of reproduction
(otherwise the plants would live forever).

\begin{code}
run :: [Plant] -> StateT (SimpleUniverse Plant) IO [Plant]
run (me:other:_) = do
  name <- genName
  (Just baby) <- liftIO $ evalRandIO (makeOffspring me other name)
  writeToLog $ 
    plantName me ++ " and " ++ plantName other ++
      " gave birth to " ++ name ++ ", with " ++ 
       show (plantFlowerColour baby) ++ " flowers"
  return [deductMatingEnergy me, deductMatingEnergy other, baby]
run x = return x -- need two agents to mate
\end{code}

The complete code listing is below.
\label{code:plant}
\includeSource{src/Tutorial/Chapter7/Plant.hs}

