We'll only use one species in this example, and it will be a very simple 
species called |Rock|.
Rocks don't reproduce, so we don't need to worry about genetics.

A complete listing of the source code discussed here is provided on 
page \pageref{code:rock}.
We'll discuss the main points here.
The |DeriveGeneric| pragma activates support for generics
(available beginning with GHC 7.2 or the Haskell Platform 2012.4.0.0),
which allows some code to be generated automatically for us.

\begin{code}
{-# LANGUAGE DeriveGeneric #-}
\end{code} 

|Rock|s have a unique ID, and a counter. 
We will use the counter to demonstrate that the Créatúr
framework persists (``remembers'') the state of the rock
from one run to the next.

\begin{code}
data Rock = Rock String Int deriving (Show, Generic)
\end{code} 

All agents used in the Créatúr framework must be an instance of
|Serialize|, which ensures that they can be written to and read from a
database or file system.
Notice that we do not need to write |put| and |get| functions.
Deriving |Generic| instructs Haskell to generate them for us.

\begin{code}
instance Serialize Rock
\end{code} 

All species used in the Créatúr framework must be an instance of 
|ALife.Creatur.Agent|,
which requires us to implement two functions: |agentId| and |isAlive|.
The function |agentId| returns a unique identifier for this agent.
The function |isAlive| indicates whether the agent is currently alive
(if it is not alive, it would automatically be archived).
Since rocks never die, this function can simply return |True|.

\begin{code}
instance Agent Rock where
  agentId (Rock name _) = name
  isAlive _ = True
\end{code} 

Making |Rock| an instance of |ALife.Creatur.Database.Record|
allows us to store agents and retrieve them from the file system.
A record needs a unique identifier;
we can use |agentId| for this purpose.

\begin{code}
instance Record Rock where key = agentId
\end{code} 

Finally, we need to write the function that will be
invoked by the daemon when it is the agent's turn to use the CPU.
This function is passed as a configuration parameter to the daemon,
as will be seen in Section \ref{sec:daemon1}.
This example is quite simple;
it merely writes a message to the log file
and returns a new version of the rock, with an updated counter.
However, this is where your agents get a chance to eat, mate, 
and do whatever else they need to do.
We'll see a more typical implementation in Section
\ref{sec:species2}.

\begin{code}
run :: Rock -> StateT (SimpleUniverse a) IO Rock
run (Rock name k) = do
  writeToLog $ name ++ "'s turn. counter=" ++ show k 
  return $ Rock name (k+1)
\end{code}

The type signature of |run| is explained in Section \ref{sec:daemon1}.
The complete code listing is below.
\label{code:rock}
\includeSource{src/Tutorial/Chapter5/Rock.hs}

